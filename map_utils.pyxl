// Scripts, variables, and constants here are visible to all modes

const MAP_LAYER_FLOOR = 0
const MAP_LAYER_OBJECTS = 1

const MAP_SPRITE_EMPTY = gameplay_sprite.empty

def create_map_from_def(map_def):
    const unpadded_tiles = replace(trim_spaces(map_def.tiles), {" ":"", "\t":""})
    const map_rows = split(unpadded_tiles, "\n")
    const map_size = xy(size(map_rows[0]), size(map_rows))
    
    let map = deep_clone(template_map)
    assert(equivalent(map.size, map_size),
        "Map \"" + map_def.name + "\" dimensions " + vec_str(map_size) + " must be " + vec_str(map.size))
    let spawn_cells = []
    for mx < map_size.x:
        for my < map_size.y:
            const mc = xy(mx,my)
            // Layer 0 is all floor (so that floors are drawn under objects in layer 1)
            set_map_sprite(map, mc, map_sprite["floor-dirt-white"], MAP_LAYER_FLOOR)
            // Fill cells in layer 1 based on map contents
            const c = map_rows[my][mx]
            if      c == ".": set_map_sprite(map, mc, MAP_SPRITE_EMPTY, MAP_LAYER_OBJECTS)
            else if c == "X": set_map_sprite(map, mc, map_sprite["wall-brick-red"], MAP_LAYER_OBJECTS)
            else if c == "x": set_map_sprite(map, mc, map_sprite["wall-brick-red"], MAP_LAYER_OBJECTS)
            else if c == "o": set_map_sprite(map, mc, map_sprite["crate-chest-brown"], MAP_LAYER_OBJECTS)
            else if c == "P":
                push(spawn_cells, mc)
                set_map_sprite(map, mc, MAP_SPRITE_EMPTY, MAP_LAYER_OBJECTS)
            else:
                assert(false, "Map \"" + map_def.name + "\" has invalid token \""+c+"\" at " + vec_str(mc))
            
    map.info = {spawn_cells: spawn_cells,
        name: map_def.name,
        description: map_def.description,
        players_min: map_def.players_min,
        players_max: map_def.players_max,
        wall_sprite: map_sprite["wall-brick-red"],
        floor_sprite: map_sprite["floor-dirt-white"],
        crate_sprite: map_sprite["crate-chest-brown"],
        crate_destroyed_sprite: map_sprite["crate-chest-brown-destroyed"],
    }
    return map