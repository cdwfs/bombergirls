// Scripts, variables, and constants here are visible to all modes

const MAP_LAYER_FLOOR = 0
const MAP_LAYER_OBJECTS = 1

const MAP_SPRITE_EMPTY = map_tiles_sprite.empty

def create_map_from_def(map_def):
    const unpadded_tiles = replace(trim_spaces(map_def.tiles), {" ":"", "\t":""})
    const map_rows = split(unpadded_tiles, "\n")
    const map_size = xy(size(map_rows[0]), size(map_rows))
    
    let map = deep_clone(template_map)
    assert(equivalent(map.size, map_size),
        "Map \"" + map_def.name + "\" dimensions " + vec_str(map_size) + " must be " + vec_str(map.size))
    let spawn_cells = []
    for mx < map_size.x:
        for my < map_size.y:
            const mc = xy(mx,my)
            // Layer 0 is all floor (so that floors are drawn under objects in layer 1)
            set_map_sprite(map, mc, map_tiles_sprite["floor"], MAP_LAYER_FLOOR)
            // Fill cells in layer 1 based on map contents
            const c = map_rows[my][mx]
            let s = âˆ…
            if      c == ".": s = "empty"
            else if c == "X": s = "wall_outer"
            else if c == "x": s = "wall_inner"
            else if c == "o": s = "crate"
            else if c == "P":
                push(spawn_cells, mc)
                s = "empty"
            else:
                assert(false, "Map \"" + map_def.name + "\" has invalid token \""+c+"\" at " + vec_str(mc))
            set_map_sprite(map, mc, map_tiles_sprite[s], MAP_LAYER_OBJECTS)
    map.info = {spawn_cells: spawn_cells,
        name: map_def.name,
        description: map_def.description,
        players_min: map_def.players_min,
        players_max: map_def.players_max,
    }
    return map